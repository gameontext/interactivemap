apply plugin: 'eclipse'

buildscript {
  repositories {
      mavenCentral()
  }

  dependencies {
    classpath 'com.bmuschko:gradle-docker-plugin:2.6'
  }
}

def dockerHost = "https://localhost:2376"
if (null != System.getenv("DOCKER_HOST"))
{
  dockerHost = System.getenv('DOCKER_HOST')

  if (dockerHost.indexOf('tcp') == 0)
  {
    dockerHost = dockerHost.replace('tcp','https')
  }
}

def dockerCertPath = ""
if (null != System.getenv('DOCKER_CERT_PATH')){
  dockerCertPath = System.getenv('DOCKER_CERT_PATH')
}

def localEnvironment = ["LICENSE=accept",
      "service_concierge=" + System.getenv('SERVICE_CONCIERGE'),
      "service_room=" + System.getenv('SERVICE_ROOM'),
      "REGISTRATION_SECRET=" + System.getenv('REGISTRATION_SECRET')]

def etcdEnvironment = ["LICENSE=accept",
              "ETCDCTL_ENDPOINT=http://etcd:4001"]

task copyTask(type: Copy) {
    from '../interactivemap-app/build/libs/interactivemap-app-1.0.war'
    into 'servers/interactivemap/apps/'
    rename("interactivemap-app-1.0.war", "interactivemap-app.war")
}

task build(dependsOn: ['copyTask']){
}

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

docker {
    url = dockerHost
    certPath = new File(dockerCertPath)
}

task buildDockerImage(type: DockerBuildImage, dependsOn: 'copyTask') {
  inputDir = file('.')
    tag = 'interactivemap'
    quiet = false
}

task stopCurrentContainer(type: DockerStopContainer) {
    targetContainerId { "interactivemap" }
    timeout 0
}

task removeCurrentContainer(type: DockerRemoveContainer) {
  targetContainerId { "interactivemap" }
}

task createNewContainer(type: DockerCreateContainer) {
  targetImageId { "interactivemap" }
  containerName = "interactivemap"
  portBindings = ['5020:9080']
  env = localEnvironment
}

task createNewEtcdContainer(type: DockerCreateContainer) {
  targetImageId { "interactivemap" }
  containerName = "interactivemap"
  portBindings = ['5090:9090']
  links = ['etcd:etcd']
  env = etcdEnvironment
}

task startNewEtcdContainer(type: DockerStartContainer) {
  dependsOn createNewEtcdContainer
  targetContainerId { "interactivemap" }
}

task startNewContainer(type: DockerStartContainer) {
  dependsOn createNewContainer
  targetContainerId { "interactivemap" }
}
